apply plugin: "application"

dependencies {
    compile "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-jsp:${jettyVersion}"

}

ext {
    distDir = "${projectDir}/dist"

    downloadUrl = "http://download.hazelcast.com/download.jsp?version=hazelcast-${hazelcastVersion}"
    zipFilePath = "${distDir}/hazelcast-${hazelcastVersion}.zip"
    warFilePath = "${distDir}/mancenter-${hazelcastVersion}.war"

    mainClass = "com.u6f6o.apps.hazelnate.mancenter.MancenterBootstrap"
    defaultJvmArgs = [ "-Dhazelcast.logging.type=slf4j" ]
}

clean{
    delete zipFilePath
    delete warFilePath
}

task downloadZip(group: "hazelnate") {
    outputs.file file(zipFilePath)
    logging.setLevel(LogLevel.INFO)
    doLast {
        ant.get(src: downloadUrl, dest: zipFilePath)
    }
}

task extractWar(dependsOn: downloadZip, group: "hazelnate") {
    outputs.file file(warFilePath)
    logging.setLevel(LogLevel.INFO)
    doLast {
        ant.unzip(src: zipFilePath, dest: distDir, overwrite:"true") {
            patternset() {
                include(name: '**/mancenter*.war')
            }
            mapper(type:"flatten")
        }
    }
}

task startMancenter(dependsOn: extractWar, type: JavaExec, group: "hazelnate") {
    main mainClass
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = defaultJvmArgs
    args warFilePath
}