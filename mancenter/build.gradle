plugins {
    id "java"
    id "idea"
    id "de.undercouch.download" version "1.2"
}

def props = new Properties()
new File(projectDir.getParent(), 'libs.properties').withInputStream {
    stream -> props.load(stream)
}
for(prop in props) {
    project.ext[prop.key] = prop.value
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        force "org.slf4j:slf4j-api:${slf4jVersion}"
        force "org.glassfish:javax.el:3.0.0"
    }
}

dependencies {
    compile "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-jsp:${jettyVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    compile "org.slf4j:log4j-over-slf4j:${slf4jVersion}"
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
}

ext {
    distDir = "${projectDir}/dist"

    downloadUrl = "http://download.hazelcast.com/download.jsp?version=hazelcast-${hazelcastVersion}&p="
    zipFilePath = "${distDir}/hazelcast-${hazelcastVersion}.zip"
    warFilePath = "${distDir}/mancenter-${hazelcastVersion}.war"

    defaultJvmArgs = [ "-Dhazelcast.logging.type=slf4j" ]
}

clean{
    delete zipFilePath
    delete warFilePath
}

task downloadZip(group: "hazelnate", type: de.undercouch.gradle.tasks.download.Download) {
    outputs.file file(zipFilePath)
    src downloadUrl
    dest zipFilePath

    doFirst {
        new File(distDir).mkdirs()
    }
}

task extractWar(dependsOn: downloadZip, group: "hazelnate") {
    outputs.file file(warFilePath)
    logging.setLevel(LogLevel.INFO)
    doLast {
        ant.unzip(src: zipFilePath, dest: distDir, overwrite:"true") {
            patternset() {
                include(name: '**/mancenter*.war')
            }
            mapper(type:"flatten")
        }
    }
}

task startMancenter(dependsOn: extractWar, type: JavaExec, group: "hazelnate") {
    main mainClazz
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = defaultJvmArgs
    args warFilePath
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.1"
}

extractWar.dependsOn downloadZip
